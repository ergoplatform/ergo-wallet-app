plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "org.ergoplatform.android"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 2212
        versionName "1.10.2212"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }

    flavorDimensions "nodetype"
    productFlavors {
        ergotestnet {
            // Assigns this product flavor to the "version" flavor dimension.
            // If you are using only one dimension, this property is optional,
            // and the plugin automatically assigns all the module's flavors to
            // that dimension.
            dimension "nodetype"
            applicationIdSuffix ".testnet"
            versionNameSuffix "-testnet"
        }
        ergomainnet {
            dimension "nodetype"
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
}

dependencies {
    implementation project(path: ':common-jvm')

    def room_version = "2.4.0"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.paging:paging-runtime:3.0.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation("androidx.cardview:cardview:1.0.0")
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation("androidx.biometric:biometric:1.1.0")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation("androidx.room:room-ktx:$room_version")
    kapt "androidx.room:room-compiler:$room_version"

    implementation('com.journeyapps:zxing-android-embedded:4.2.0')

    // newer okhttp version than pulled from dependencies to avoid StrictMode policy violation
    // defined here because causing errors on iOS
    implementation("com.squareup.okhttp3:okhttp:3.12.13")
    implementation("com.squareup.okhttp3:okhttp-urlconnection:3.12.13")

    // work around guava duplicate classes with guava 20.0 (fixed in 27+ but not usable)
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    // Compose
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "com.github.MrStahlfelge.mosaik:common-compose:$mosaik_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}

// Disable dependency verification on test classpaths
configurations {
    ergomainnetDebugAndroidTestRuntimeClasspath {
        resolutionStrategy {
            disableDependencyVerification()
        }
    }
    ergomainnetDebugAndroidTestCompileClasspath {
        resolutionStrategy {
            disableDependencyVerification()
        }
    }
    ergotestnetDebugAndroidTestRuntimeClasspath {
        resolutionStrategy {
            disableDependencyVerification()
        }
    }
    ergotestnetDebugAndroidTestCompileClasspath {
        resolutionStrategy {
            disableDependencyVerification()
        }
    }
}
