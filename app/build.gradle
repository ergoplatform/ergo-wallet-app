plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.ergoplatform.android"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 2111
        versionName "1.1.2111"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }

    flavorDimensions "nodetype"
    productFlavors {
        ergotestnet {
            // Assigns this product flavor to the "version" flavor dimension.
            // If you are using only one dimension, this property is optional,
            // and the plugin automatically assigns all the module's flavors to
            // that dimension.
            dimension "nodetype"
            applicationIdSuffix ".testnet"
            versionNameSuffix "-testnet"
        }
        ergomainnet {
            dimension "nodetype"
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}


repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }

//    ivy {
//        url = "${System.getProperties().getProperty("user.home")}/.ivy2/local"
//        layout("ivy")
//        patternLayout {
//            artifactPattern("${url}/[organisation]/[module]/[revision]/[type]s/[artifact](-[classifier])(.[ext])")
//        }
//    }
}

dependencies {
    def room_version = "2.3.0"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation("androidx.cardview:cardview:1.0.0")
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation("androidx.biometric:biometric:1.1.0")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"
    implementation ('org.ergoplatform:ergo-appkit_2.11:develop-71dbc403-SNAPSHOT') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
        exclude group: 'org.bitbucket.inkytonik.kiama', module: 'kiama_2.11'
    }
    // kiama needs an own build for JRE7 compatibility
    implementation "com.github.MrStahlfelge.kiama:core_2.11:2.1.1"
    implementation "com.github.MrStahlfelge.kiama:library_2.11:2.1.1"
    implementation "org.bouncycastle:bcprov-jdk15to18:1.66"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation("androidx.room:room-ktx:$room_version")
    kapt "androidx.room:room-compiler:$room_version"

    implementation('com.journeyapps:zxing-android-embedded:4.2.0')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
}
